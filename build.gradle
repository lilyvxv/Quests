plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.2.2'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.gradle.maven-publish'
}

group = 'me.github.lilyvxv'
version = '2.1.3'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "codemc"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "lucko"
        url = "https://repo.lucko.me/"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'me.github.lilyvxv'
            artifactId = 'Quests'
            version = version
        }
    }
    repositories {
        maven {
            url uri('build/repo')
        }
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20-R0.1-SNAPSHOT"

    compileOnly "dev.jorel:commandapi-bukkit-core:9.3.0"
    implementation "dev.jorel:commandapi-bukkit-shade:9.3.0"

    implementation 'de.tr7zw:item-nbt-api:2.11.3'

    implementation 'me.lucko:helper:5.6.14'

    implementation 'org.mongodb:mongodb-driver-sync:4.11.1'

}

shadowJar {
    dependencies {
        include dependency('dev.jorel:commandapi-bukkit-shade:9.3.0')
        include dependency('org.mongodb:bson')
        include dependency('org.mongodb:mongo-java-driver')
        include dependency('org.mongodb:mongodb-driver-core')
        include dependency('org.mongodb:mongodb-driver-sync')

        include dependency('me.lucko:helper')
    }

    relocate 'me.lucko.helper', 'me.github.lilyvxv.helper'
    relocate 'dev.jorel.commandapi', 'me.github.lilyvxv.commandapi'
    relocate 'de.tr7zw.changeme.nbtapi', 'me.github.lilyvxv.nbtapi'
}

build {
    dependsOn(shadowJar)
}

tasks {
    runServer {
        minecraftVersion("1.20")
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
